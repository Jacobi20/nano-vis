/*  This file downloaded from Highend3d.com
''  
''  Highend3d.com File Information:
''  
''    Script Name: targetWeld v0.0
''    Author: campbell strong
''    Last Updated: March 10, 2003
''    Update/Change this file at:
''    http://www.highend3d.com/maya/mel/?section=polygon#2194
''  
''  Please do not alter any information above this line
''  it is generated dynamically by Highend3d.com and will
''  be changed automatically on any updates.
*/

/*

targetWeld
---------------
- emulates the behaviour of the 3dsMax tool -  snap selected
 vertex to location of a second vert, and weld/merge them. 

Campbell Strong 9/02 4.0 > 02/03 4.5
campbellstrong@hotmail.com 
-----------------------------------------

Usage:
---------
- invoke the tool with "targetWeld" proc (stick  it in a shelf 
button, hotkey, MMenu).

- follow help line prompts:
	- select a single vertex initially,
	- followed by the second vertex which will be
	snapped and merged/welded to.
	- or simply shift select to weld last weld to
	new target.

- the tool will error if you select a multiplicity of points at 
any selection stage:
         -->tool is geared for quickly snaping one vert to one, unbroken 
by pressing enter, you dont get the 'multiple pnts all snapping to
one' functionality. just shift-select your 'multiple pnts' one by one
into one, then take them to their destinatn (its quick). if you've got 
a whole bunch (>~10), where selectn is drags, do distance merge (or
jig this script if you use that a lot, of just have large dist merge on 
a MM).

- merge vertices does not work across independent meshes,
they should be combined.

- if you cant see pnts on your mesh at any pnt, like if you return to the
tool via 'last tool'/y or in toolBox rather than by a targetWeld call, 
simply RMB->vrts them back to visibility.

- if your on running prior to 4.5, you'll need to comment out some of 
the functionality that has been added, its just below, line 59.

*/


global proc targetWeld()
{
	if (!`scriptCtx -exists snapWeld`){
		scriptCtx
	     -t "Weld Verts"
	     -tss 1
	     -fcs "vrtSnapAndWeld($Selection1)"
	     -esl 1
	     -snp "       Select FIRST, singular, vtx to snap and weld to a second, of shift select for SECOND w last weld as first"
	     -ssp "        Select SECOND, singular, vtx to snap and weld to"
	     -setDoneSelectionPrompt " ****  --->  MORE THAN ONE PNT SELECTED FOR SNAP AND WELD, UNSELECT THESE AND TRY AGAIN  <---  ****"
	     -sat 1
	     -ssc 2
	     -sac 1
	     -pv 1
	                        
	     -euc 0
	     
	     
	     // maya 4.5 specific additions, comment "//" out if your on 4.0 (just like this line).
	     -tct "edit"
	     -ts "$sel = `ls -sl -dag -typ mesh`; select -cl; for ($se in $sel) doMenuComponentSelection($se, \"pv\");"
	     // end 4.5 specific additions
	         
	     snapWeld;
	}

	$sel = `ls -sl -dag -typ mesh`;
	select -cl;
	for ($se in $sel)
	doMenuComponentSelection($se, "pv");
	setToolTo snapWeld;

}


global proc vrtSnapAndWeld(string $sel[])
{
	undo; string $fst[] = `ls -sl -fl -type float3`; 
	string $scnd[] = stringArrayRemove($fst, $sel);
	if (size($fst) == 1){
			float $p[] = `pointPosition -w $scnd`;
			move -ws $p[0] $p[1] $p[2] $fst;
			polyMergeVertex  -d 0.0001 -ch 0 $fst $scnd;
		} else {
			warning " ** More than one pnt initially selected to snap and weld to, start selectn again (you may have coincident vrts) ** ";
			select -cl;
			setToolTo snapWeld;
		}
}





