/*
	The MIT License

	Copyright (c) 2009 IFMO/GameDev Studio

	Permission is hereby granted, free of charge, to any person obtaining a copy
	of this software and associated documentation files (the "Software"), to deal
	in the Software without restriction, including without limitation the rights
	to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
	copies of the Software, and to permit persons to whom the Software is
	furnished to do so, subject to the following conditions:

	The above copyright notice and this permission notice shall be included in
	all copies or substantial portions of the Software.

	THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
	IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
	FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
	AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
	LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
	OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
	THE SOFTWARE.
*/



proc cityBlock(float $w, float $d, float $h, float $x, float $y, float $z)
{
	polyCube 
		-width  $w 
		-height $d 
		-depth  $h 
		-sx 1 -sy 1 -sz 1 
		-ax 0 1 0 
		-cuv 4 
		-ch 1;

	move -r $x $y $z;
}


proc cityBlockTop(float $w, float $d, float $h, float $x, float $y, float $z)
{
	cityBlock($w, $d, $h, $x, $y, $z + 0.5 * $h);
}

proc cityBlockBottom(float $w, float $d, float $h, float $x, float $y, float $z)
{
	cityBlock($w, $d, $h, $x, $y, $z - 0.5 * $h);
}



proc cityMakeSkyscraper(int $floor_num)

{
	float	$base_x 		= 16;
	float 	$base_y 		= 16;
	float 	$floor_height	= 3;
	float	$wall_thick		= 0.25;
	float	$current_height	= 0;
	int 	$i;

	//	build the foundation :
	cityBlock($base_x-1, $base_y-1, 2, 0, 0, 1);
	$current_height	+= 2;
	
	//	build 4 main support column (they go through entire building) :
	cityBlockTop(1, 1, $floor_num * $floor_height,  $base_x*0.25,  $base_y*0.25, $current_height);
	cityBlockTop(1, 1, $floor_num * $floor_height,  $base_x*0.25, -$base_y*0.25, $current_height);
	cityBlockTop(1, 1, $floor_num * $floor_height, -$base_x*0.25,  $base_y*0.25, $current_height);
	cityBlockTop(1, 1, $floor_num * $floor_height, -$base_x*0.25, -$base_y*0.25, $current_height);

	//	minor supports :
	for ($i=0; $i < 4; $i++) {
		float $d  =  $wall_thick;
		float $hd = 0.5 * $wall_thick;
		
		float $dx = 0.125 * $base_x;
		float $dy = 0.125 * $base_y;
		
		cityBlockTop($hd, $d, $floor_num * $floor_height,   0.5 * $base_x - $hd,   $dy * ($i+0.5), $current_height);
		cityBlockTop($hd, $d, $floor_num * $floor_height,   0.5 * $base_x - $hd,  -$dy * ($i+0.5), $current_height);
		cityBlockTop($hd, $d, $floor_num * $floor_height,  -0.5 * $base_x + $hd,   $dy * ($i+0.5), $current_height);
		cityBlockTop($hd, $d, $floor_num * $floor_height,  -0.5 * $base_x + $hd,  -$dy * ($i+0.5), $current_height);
		
		cityBlockTop($d, $hd, $floor_num * $floor_height,   $dx * ($i+0.5),    0.5 * $base_y - $hd,$current_height);
		cityBlockTop($d, $hd, $floor_num * $floor_height,  -$dx * ($i+0.5),    0.5 * $base_y - $hd,$current_height);
		cityBlockTop($d, $hd, $floor_num * $floor_height,   $dx * ($i+0.5),   -0.5 * $base_y + $hd,$current_height);
		cityBlockTop($d, $hd, $floor_num * $floor_height,  -$dx * ($i+0.5),   -0.5 * $base_y + $hd,$current_height);
	}                                               
	
	//	make floors :
	for ($i=0; $i < $floor_num; $i++) {
		float $d  =  $wall_thick;
		float $hd = 0.5 * $wall_thick;

		//	floor :
		cityBlockTop($base_x, $base_y, $wall_thick, 0, 0, $current_height);

		//	facial walls and windows :
		cityBlockTop($base_x, $d, 1.0-$hd,	 	 0,  0.5 * $base_y - $hd, 	 $current_height+$d);
		cityBlockTop($base_x, $d, 1.0-$hd,	 	 0, -0.5 * $base_y + $hd, 	 $current_height+$d);
		cityBlockTop($d, $base_y, 1.0-$hd,	  		 0.5 * $base_y - $hd, 0, $current_height+$d);
		cityBlockTop($d, $base_y, 1.0-$hd,     	 	-0.5 * $base_y + $hd, 0, $current_height+$d);
		
		cityBlockBottom($base_x, $d, 0.5-$hd,	 0,  0.5 * $base_y - $hd, 	 $current_height+$floor_height);
		cityBlockBottom($base_x, $d, 0.5-$hd, 	 0, -0.5 * $base_y + $hd, 	 $current_height+$floor_height);
		cityBlockBottom($d, $base_y, 0.5-$hd,		 0.5 * $base_y - $hd, 0, $current_height+$floor_height);
		cityBlockBottom($d, $base_y, 0.5-$hd,   	-0.5 * $base_y + $hd, 0, $current_height+$floor_height);
		
		
		
		                                                         
		$current_height += $floor_height;
	
	}
	
	//	make roof :
	cityBlockTop($base_x, $base_y, $wall_thick, 0, 0, $current_height);
	
}

cityMakeSkyscraper(5);









